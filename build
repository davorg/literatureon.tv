#!/usr/bin/perl

use strict;
use warnings;

use Template;
use File::Copy::Recursive 'dircopy';
use Literature::Schema;

my %resources = qw[
  actors      Actor
  authors     Author
  characters  Character
  productions Production
  works       Work
];

my $tt = Template->new({
  INCLUDE_PATH => [ qw( in tt_lib ) ],
  OUTPUT_PATH  => 'docs',
  WRAPPER      => 'main.tt',
});

my $sch = Literature::Schema->get_schema;

$tt->process('index.tt', {}, 'index.html')
  or die $tt->error;

for (keys %resources) {
  print "Processing $_\n";

  my $rs = $sch->resultset($resources{$_});

  $tt->process("$_.tt", { $_ => $rs }, "$_/index.html")
    or die $tt->error;

  foreach my $obj ($rs->all) {
    my $singular = s/s$//r;

    $tt->process("$singular.tt", { $singular => $obj },
                 "$_/" . $obj->slug . '/index.html')
      or die $tt->error;
  }
}

dircopy('static', 'docs');
