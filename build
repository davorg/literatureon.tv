#!/usr/bin/perl

use strict;
use warnings;

use Template;
use File::Copy::Recursive 'dircopy';
use Time::Piece;
use Literature::Schema;

my %resources = qw[
  actors      Actor
  authors     Author
  characters  Character
  productions Production
  works       Work
];

my $tt = Template->new({
  INCLUDE_PATH => [ qw( in tt_lib ) ],
  OUTPUT_PATH  => 'docs',
  PRE_PROCESS  => 'header.tt',
  POST_PROCESS => 'footer.tt',
});

my $sch = Literature::Schema->get_schema;

$tt->process('index.tt', {}, 'index.html')
  or die $tt->error;

my $vars = {
  url => 'https://literatureon.tv',
  date => localtime->strftime('%Y-%m-%d'),
};

for (keys %resources) {
  print "Processing $_\n";

  my $rs = $sch->resultset($resources{$_});

  $tt->process("$_.tt", { $_ => $rs }, "$_/index.html")
    or die $tt->error;

  foreach my $obj ($rs->all) {
    my $singular = s/s$//r;

    $tt->process("$singular.tt", { $singular => $obj },
                 "$_/" . $obj->slug . '/index.html')
      or die $tt->error;

    push @{$vars->{$_}}, $obj;
  }
}

$tt = Template->new({
  INCLUDE_PATH => [ qw( in tt_lib ) ],
  OUTPUT_PATH  => 'docs',
});

$tt->process('sitemap.tt', $vars, 'sitemap.xml')
  or die $tt->error;

dircopy('static', 'docs');
